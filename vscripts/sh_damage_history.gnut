
global function DamageHistoryInit
global function StoreDamageHistoryAndUpdate
global function WasRecentlyHitForDamage
global function WasRecentlyHitByEntity
global function WasRecentlyHitByEntityWithDamagetype
global function WasRecentlyHitByDamageSourceId
global function GetLatestAssistingPlayerInfo
global function GetLatestAssistingPlayerFromSameTeam
global function GetTotalDamageTakenInTime
global function GetTotalDamageTaken
global function GetTotalDamageTakenByPlayer
global function GetDamageSortedByAttacker
global function GetTitansHitMeInTime
global function GetLastDamageTime
global function GetDamageEventsForTime
global function WasRecentlyHitForDamageType
global function GetRodeoAttacksByPlayer
global function GetLastDamageSourceStringForAttacker
global function TotalDamageOverTime_BlendedOut
global function ClearRecentDamageHistory
global function AttackerDamagesSort
global function PrintDamageEventsForTime

global struct AttackerDamage
{
	entity attacker
	string attackerName
	float totalDamage
	table<int,float> damageBySource
}

global enum eDamageHistorySet
{
	DEFAULT,
	HEALTH,
	SHIELD,
	BLEEDOUT,
	ALL,
}

global struct DeathRecapDamageData
{
	int   attackerEHandle
	int   victimEHandle
	int   damageSourceID
	int   damageType
	int   totalDamage
	int   hitCount
	int   headShotBits
	float healthFrac
	float shieldFrac
	float blockTime

	int gladCardSlotIndex
	asset customImage
}

global struct DamageHistoryStruct
{
	string attackerName
	string attackerPetName
	vector origin
	float damage
	int damageType
	int damageSourceId
	entity attacker
	int attackerEHandle
	float attackerHealthPercent
	float time
	array<string> weaponMods
	bool victimIsTitan
	bool rodeoDamage
}

global struct AssistingPlayerStruct
{
	entity player
	int damageSourceId
	float assistTime
}

void function DamageHistoryInit()
{
}

void function ClearRecentDamageHistory( entity player )
{
	player.e.recentDamageHistory.clear()
}

DamageHistoryStruct function StoreDamageHistoryAndUpdate( entity storeEnt, float maxTime, float damage, vector damageOrigin, int damageType, int damageSourceId, entity attacker = null, array<string> weaponMods = [] )
{
	DamageHistoryStruct damageHistory

	float time = Time()

	#if SERVER
		if ( !attacker.IsPlayer() )
		{
			entity newAttacker = GetPlayerFromEntity( attacker )
			if ( IsValid_ThisFrame( newAttacker ) )
				attacker = newAttacker
		}
	#endif

	if ( damageType & DF_VORTEX_REFIRE )
		damageSourceId = eDamageSourceId.damagedef_unknownBugIt

	if ( IsValid( attacker ) )
	{
		#if CLIENT
			AttackerDisplayNameStruct names = GetAttackerDisplayNamesFromClassname( attacker, true )
			damageHistory.attackerName = names.attackerName
			damageHistory.attackerPetName = names.attackerPetName
			damageHistory.attackerHealthPercent = max( 0.0, float( attacker.GetHealth() ) / float( attacker.GetMaxHealth() ) * 100.0 )
		#endif
	}

	damage = min( damage, storeEnt.GetHealth() )
	damageHistory.origin = damageOrigin
	damageHistory.damage = damage
	damageHistory.damageType = damageType
	damageHistory.damageSourceId = damageSourceId
	damageHistory.attacker = attacker
	damageHistory.time = time
	damageHistory.weaponMods = weaponMods

	// clear the damage history
	if ( GetHealthFrac( storeEnt ) == 1.0 )
		maxTime = 0

	damageHistory.victimIsTitan = false

	if ( maxTime >= 0 )
		UpdateDamageHistory( storeEnt, maxTime, time )

	storeEnt.e.recentDamageHistory.insert( 0, damageHistory )

	return damageHistory
}

float function GetLastDamageTime( entity player )
{
	if ( !player.e.recentDamageHistory.len() )
		return -1.0

	return player.e.recentDamageHistory[ 0 ].time
}

array<DamageHistoryStruct> function GetDamageEventsForTime( entity player, float hitTime )
{
	float time = Time() - hitTime

	array<DamageHistoryStruct> events

	foreach ( event in player.e.recentDamageHistory )
	{
		if ( event.time < time )
			return events

		events.insert( 0, event )
	}

	return events
}

void function PrintDamageEventsForTime( entity player, float time = 20.0 )
{
	array<DamageHistoryStruct> events = GetDamageEventsForTime( player, time )

	foreach ( event in events )
	{
		printt( "event:", event.damage, event.damageType, event.damageSourceId, event.attacker )
	}
}


void function UpdateDamageHistory( entity player, float maxTime, float time )
{
	// remove old damage history entries
	int i
	float removeTime = time - maxTime

	for ( i = player.e.recentDamageHistory.len() - 1; i >= 0; i-- )
	{
		DamageHistoryStruct history = player.e.recentDamageHistory[ i ]

		if ( history.time > removeTime )
			return

		player.e.recentDamageHistory.remove( i )
	}
}

bool function WasRecentlyHitByEntity( entity player, entity ent, float hitTime )
{
	float time = Time() - hitTime
	foreach ( DamageHistoryStruct history in player.e.recentDamageHistory )
	{
		if ( history.time < time )
			continue
		if ( history.attacker == ent )
			return true
	}

	return false
}

bool function WasRecentlyHitByEntityWithDamagetype( entity player, entity ent, int damageType, float hitTime )
{
	float time = Time() - hitTime
	foreach ( DamageHistoryStruct history in player.e.recentDamageHistory )
	{
		if ( history.attacker != ent )
			continue
		if ( !(history.damageType & damageType) )
			continue
		if ( history.time < time )
			continue

		return true
	}

	return false
}

bool function WasRecentlyHitForDamage( entity player, float damageAmount, float hitTime )
{
	float time = Time() - hitTime
	// remove old damage history entries
	int i

	foreach ( history in player.e.recentDamageHistory )
	{
		if ( history.time < time )
			continue

		if ( history.damage >= damageAmount )
			return true
	}

	return false
}

bool function WasRecentlyHitForDamageType( entity player, int damageType, float hitTime )
{
	float time = Time() - hitTime
	// remove old damage history entries
	int i

	foreach ( history in player.e.recentDamageHistory )
	{
		if ( history.time < time )
			continue
		if ( history.damageType & damageType )
			return true
	}

	return false
}

float function GetTotalDamageTaken( entity player )
{
	float total = 0
	foreach ( history in player.e.recentDamageHistory )
	{
		total += history.damage
	}

	return total
}

float function GetTotalDamageTakenInTime( entity player, float hitTime )
{
	float time = Time() - hitTime
	// remove old damage history entries
	int i

	float total = 0
	foreach ( history in player.e.recentDamageHistory )
	{
		if ( history.time < time )
			continue

		total += history.damage
	}

	return total
}

array<entity> function GetTitansHitMeInTime( entity player, float hitTime )
{
	float time = Time() - hitTime
	// remove old damage history entries
	array<entity> titans

	foreach ( history in player.e.recentDamageHistory )
	{
		if ( history.time < time )
			continue

		if ( !IsValid( history.attacker ) )
			continue
		if ( !history.attacker.IsTitan() )
			continue
		if ( titans.contains( history.attacker ) )
			continue

		titans.append( history.attacker )
	}

	return titans
}

float function GetTotalDamageTakenByPlayer( entity player, entity attacker )
{
	Assert( IsValid( attacker ) )
	// remove old damage history entries
	int i

	float total = 0
	foreach ( history in player.e.recentDamageHistory )
	{
		if ( history.attacker != attacker )
			continue

		total += history.damage
	}

	return total
}

array<AttackerDamage> function GetDamageSortedByAttacker( entity ent, float totalTime )
{
	Assert( !ent.IsTitan(), "Pass titan soul, not titan itself" )

	float firstCheckTime = Time() - totalTime
	table<entity,AttackerDamage> attackerDamagesTable
	array<AttackerDamage> attackerDamages

	foreach ( history in ent.e.recentDamageHistory )
	{
		if ( history.attacker == null )
			continue

		if ( history.time < firstCheckTime )
			continue

		AttackerDamage attackerDamage
		if ( history.attacker in attackerDamagesTable )
		{
			attackerDamage = attackerDamagesTable[ history.attacker ]
		}
		else
		{
			attackerDamagesTable[ history.attacker ] <- attackerDamage

			attackerDamage.attacker = history.attacker
			attackerDamages.append( attackerDamage )
		}

		if ( !( history.damageSourceId in attackerDamage.damageBySource ) )
		{
			attackerDamage.damageBySource[ history.damageSourceId ] <- 0
		}

		attackerDamage.damageBySource[ history.damageSourceId ] += history.damage
		attackerDamage.totalDamage += history.damage
	}

	attackerDamages.sort( AttackerDamagesSort )

	return attackerDamages
}

int function AttackerDamagesSort( AttackerDamage a, AttackerDamage b )
{
	if ( a.totalDamage > b.totalDamage )
		return -1
	if ( b.totalDamage > a.totalDamage )
		return 1
	return 0
}

bool function WasRecentlyHitByDamageSourceId( entity player, int damageSourceId, float hitTime )
{
	float time = Time() - hitTime

	// remove old damage history entries
	foreach ( DamageHistoryStruct history in player.e.recentDamageHistory )
	{
		if ( history.time < time )
			continue

		if ( history.damageSourceId == damageSourceId )
			return true
	}

	return false
}


// Used to get the player that did the last damage in cases where an NPC, not associated with a player, gives the killing blow.
// at this point not every entity uses e.recentDamageHistory so some entities won't give assist points when they die ( soldiers, turrets etc. )
AssistingPlayerStruct function GetLatestAssistingPlayerInfo( entity ent )
{
	float lastTime = 0.0
	float currentTime = Time()
	AssistingPlayerStruct assistingPlayerInfo

	foreach ( history in ent.e.recentDamageHistory )
	{
		if ( !IsValid( history.attacker ) || !history.attacker.IsPlayer() )
			continue

		if ( history.time <= lastTime )
			continue

		if ( currentTime - history.time >= MAX_NPC_KILL_STEAL_PREVENTION_TIME )
			continue

		lastTime = history.time
		assistingPlayerInfo.player = history.attacker
		assistingPlayerInfo.damageSourceId = history.damageSourceId
		assistingPlayerInfo.assistTime = history.time
	}

	return assistingPlayerInfo
}

array<entity> function GetLatestAssistingPlayerFromSameTeam( entity victim, entity attacker )
{
	float lastTime = 0.0
	float currentTime = Time()
	array<entity> assistingPlayerArray

	foreach ( history in victim.e.recentDamageHistory )
	{
		if ( !IsValid( history.attacker ) || !history.attacker.IsPlayer() )
			continue

		if ( history.attacker == attacker )
			continue

		if ( history.attacker.GetTeam() != attacker.GetTeam() )
			continue

		if ( currentTime - history.time >= MAX_ASSIST_TIME_GAP )
			continue

		if ( assistingPlayerArray.contains( history.attacker ) )
			continue

		assistingPlayerArray.append( history.attacker )
	}

	return assistingPlayerArray
}


array<DamageHistoryStruct> function GetRodeoAttacksByPlayer( entity player, entity attacker, float time )
{
	float validTime = Time() - time
	array<DamageHistoryStruct> events

	if ( !IsValid( player ) )
		return events

	if ( !player.IsTitan() )
		return events

	if ( !IsValid( attacker ) )
		return events

	entity soul = player.GetTitanSoul()
	if ( !IsValid( soul ) )
		return events

	foreach ( history in soul.e.recentDamageHistory )
	{
		if ( history.time < validTime )
			continue

		if ( history.attacker != attacker )
			continue

		// if ( !(history.damageType & DF_RODEO) )
		// 	continue

		events.append( history )
	}

	return events
}

string function GetLastDamageSourceStringForAttacker( entity victim, entity attacker )
{
	if ( victim.IsTitan() )
		victim = victim.GetTitanSoul()

	if ( !IsValid( victim ) )
		return ""

	array<DamageHistoryStruct> recentDamageHistory = victim.e.recentDamageHistory
	foreach ( damageHistory in recentDamageHistory )
	{
		if ( !IsValid( damageHistory.attacker ) || !damageHistory.attacker.IsPlayer() )
			continue

		if ( damageHistory.attacker == attacker )
			return DamageSourceIDToString( damageHistory.damageSourceId )
	}

	return ""
}

float function TotalDamageOverTime_BlendedOut( entity soul, float start, float end )
{
	// rev 3 with damage history
	float totalAcquiredDamage = 0
	float time = Time()
	float ignoreTime = time - end
	float blendOutStarts = time - start

	foreach ( history in soul.e.recentDamageHistory )
	{
		// dont care about the history past this point
		if ( history.time < ignoreTime )
			break

		float newDamage
		if ( history.damage < 0 )
		{
			newDamage = history.damage
		}
		else
		{
			newDamage = GraphCapped( history.time, blendOutStarts, ignoreTime, history.damage, 0 )
		}

		totalAcquiredDamage += newDamage
	}

	return totalAcquiredDamage
}
